plugins {
    id 'java'
    id 'io.github.karlatemp.publication-sign' version '1.3.15'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30' apply false
}

description = "A bridge for access jdk.internal.misc.Unsafe"

allprojects {
    group 'io.github.karlatemp'
    version '1.6.2'
    repositories {
        mavenCentral()
    }


    afterEvaluate {
        pluginManager.withPlugin('org.jetbrains.kotlin.jvm') {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib'
                api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        pluginManager.withPlugin('java') {
            dependencies {
                compileOnly group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
                testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
                testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
            }

            if (project.name == 'impl.testunit') {
                return
            }
            test {
                useJUnitPlatform()
            }
            compileJava {
                if (!project.ext.has('versionOverrided')) {
                    println("Java 8 for ${project.name}")
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }
            }

            compileTestJava {
                if (!project.ext.has('versionOverrided')) {
                    sourceCompatibility = JavaVersion.VERSION_1_8
                    targetCompatibility = JavaVersion.VERSION_1_8
                }
            }
        }
    }
}

publicationSign {
    setupWorkflow { workflow ->
        workflow.fastSetup("keys/key.pub", "keys/key.pri")
    }
}
