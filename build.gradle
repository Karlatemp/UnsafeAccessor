plugins {
    id 'java'
    id("maven-publish")
}

allprojects {
    group 'io.github.karlatemp'
    version '1.0.0'
    repositories {
        mavenCentral()
    }
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    compileOnly project(':accessor')
    compile project(':java8')
    compile project(':java9')
    compile fileTree(file('impl8/dist'))
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Karlatemp/UnsafeAccessor")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
            pom {
                name = 'Unsafe Accessor'
                description = 'Unsafe accessing library'
                url = 'https://github.com/Karlatemp/UnsafeAccessor'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://spdx.org/licenses/MIT.html'
                    }
                }
            }
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    true
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }
}
